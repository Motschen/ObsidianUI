plugins {
    id 'dev.architectury.loom' version '1.2-SNAPSHOT'
    id 'io.github.juuxel.loom-vineflower' version '1.+'
    id 'java-library'
    id 'maven-publish'
}

group = project.maven_group
version = "${project.mod_version}+${getMCVersionString()}"
archivesBaseName = project.archives_base_name

// This field defines the Java version your mod target.
def targetJavaVersion = 8

def getMCVersionString() {
    return project.minecraft_version
}

loom {
    forge {
        mixinConfigs = [
                "spruceui.mixins.json","spruceui.test.mixins.json"
        ]
    }
}

repositories {
    mavenLocal()
    maven {
        name = "AperLambda"
        url = 'https://aperlambda.github.io/maven'
    }
    maven { url "https://maven.architectury.dev/" }
}

configurations {
    api.extendsFrom libApi
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"

    modImplementation("me.shedaniel:architectury-forge:${project.architectury_version}")

    libApi("org.aperlambda:lambdajcommon:1.8.1") {
        exclude group: 'com.google.code.gson'
        exclude group: 'com.google.guava'
    }
    include "org.aperlambda:lambdajcommon:1.8.1"
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release.set(targetJavaVersion)
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {

    }
}
